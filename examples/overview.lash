#!/bin/env -S lash run

@define INCLUDE_TIP true
@import "overview-note.txt" into const tip_text

enum Stage
    Todo
    Doing
    Done
end

fn stage_icon(stage)
    if stage == Stage::Todo
        return "[ ]"
    end

    if stage == Stage::Doing
        return "[~]"
    end

    return "[x]"
end

const project = "Lash Overview"
let profile = "quick"
if #argv > 0
    profile = argv[0]
end

let tasks = ["Parse", "Type-check", "Emit Bash"]
let stages = []
stages[0] = Stage::Done
stages[1] = Stage::Doing
stages[2] = Stage::Todo

switch profile
    case "quick":
        tasks += ["Smoke test"]
        stages[3] = Stage::Todo
    case "release":
        tasks += ["Bundle binaries", "Publish checksums"]
        stages[3] = Stage::Todo
        stages[4] = Stage::Todo
    case "*":
        echo $"Unknown profile '{profile}', using base tasks."
end

let timestamp = $sh "date +%H:%M:%S"
echo "[" $timestamp "]" $project "(" $profile ")"

let i = 0
while i < #tasks
    let icon = stage_icon(stages[i])
    let task_name = tasks[i]
    echo $icon $task_name
    i = i + 1
end

let done_count = 0
for stage in stages
    if stage == Stage::Done
        done_count = done_count + 1
    end
end

let stats = []
stats["total"] = #tasks
stats["done"] = done_count
let total = stats["total"]
let done = stats["done"]
echo "Progress:" $done "/" $total "complete"

subshell into lint_pid
    sh "sleep 5"
end &
echo "waiting for subshell..."
wait lint_pid into lint_status
echo "Background check exit status:" $lint_status

@if defined(INCLUDE_TIP) && INCLUDE_TIP == "true"
echo $tip_text
@end
