#!/bin/env -S lash run
// Quick runnable overview of current Lash syntax.

@define TARGET linux
@import "overview-snippet.lash"

@if defined(TARGET) && TARGET == "linux"
echo "Targetting Linux"
@else
@warning "Unknown target"
@end

// raw bash block
@raw
for i in {1..5}; do
  if [ "$i" -eq 3 ]; then
    echo "$i (middle)"
  else
    echo "$i"
  fi
done
@end

fn greet(name, greeting = "Hello")
    return greeting + ", " + name
end

fn increment(count)
    count = count + 1
    return count
end

let values = ["foo1", "bar2", "foo3", "bar4"]
let value_count = #values
values += ["baz5"]
let newest_value = values[4]
const x = 5

if x > 0
    echo "is positive"
end

for i in 0 .. 10 step 2
    echo $i
end

for thing in values
    echo $thing
end

let counter = 0
while counter < 3
    counter = counter + 1
    echo $counter
end

enum ExampleEnum
    TypeOne
    TypeTwo
    TypeThree
end

let enum_value = ExampleEnum::TypeOne
if enum_value == ExampleEnum::TypeOne
    echo "enum match!"
end

switch counter
    case 1: echo "thing1"
    case 2: echo "thing2"
    case 3: echo "thing3"
end

fn emit()
    echo "stdout-line"
    echo "stderr-line" 1>&2
end

emit() >> "overview-out" // stdout
emit() 2>> "overview-err" // stderr
emit() &>> "overview-all" //both

// pipes work on functions and variables, too
let word = "Rob"
let greeting
word | greet() | greeting
echo $greeting

// argv + shift
let first_arg = argv[0]
let arg_count = #argv
let arg_summary = $"first arg: {first_arg}, total args: {arg_count}"
echo $arg_summary

shift
let remaining_args = #argv
let shift_summary = $"after shift: {remaining_args}"
echo $shift_summary

// foreground subshell
subshell
    echo "inside subshell"
end

// subshell background + wait
let bg_pid = 0
let bg_status = 0

subshell into bg_pid
    sh "sleep 0.01"
end &

// wait for single pid
wait bg_pid into bg_status
// wait for all jobs
wait jobs into bg_status
echo $"background status: {bg_status}"

// explicit shell capture
let here = $sh "pwd"
let folder = $sh $"basename \"{here}\""
let folder_summary = $"cwd folder: {folder}"
echo $folder_summary

// string-key indexing (associative array mode)
let meta = []
meta["owner"] = "lash"
meta["cwd"] = folder
let owner = meta["owner"]
let owner_summary = $"meta owner: {owner}"
echo $owner_summary

// switch case supports glob patterns
switch first_arg
    case "win-*":
        echo "input looks like windows"
    case "linux-*":
        echo "input looks like linux"
    case "":
        echo "no first arg provided"
end
